const std = @import("std");
const fs = std.fs;

const wayland = @import("wayland");

// Although this function looks imperative, it does not perform the build
// directly and instead it mutates the build graph (`b`) that will be then
// executed by an external runner. The functions in `std.Build` implement a DSL
// for defining build steps and express dependencies between them, allowing the
// build runner to parallelize the build automatically (and the cache system to
// know when a step doesn't need to be re-run).
pub fn build(b: *std.Build) void {
    // Standard target options allow the person running `zig build` to choose
    // what target to build for. Here we do not override the defaults, which
    // means any target is allowed, and the default is native. Other options
    // for restricting supported target set are available.
    const target = b.standardTargetOptions(.{});
    // Standard optimization options allow the person running `zig build` to select
    // between Debug, ReleaseSafe, ReleaseFast, and ReleaseSmall. Here we do not
    // set a preferred release mode, allowing the user to decide how to optimize.
    const optimize = b.standardOptimizeOption(.{});
    // It's also possible to define more custom flags to toggle optional features
    // of this build script using `b.option()`. All defined flags (including
    // target and optimize options) will be listed when running `zig build --help`
    // in this directory.

    const scanner = wayland.Scanner.create(b, .{});

    scanner.addSystemProtocol("stable/viewporter/viewporter.xml");
    scanner.addSystemProtocol("staging/fractional-scale/fractional-scale-v1.xml");
    scanner.addSystemProtocol("staging/single-pixel-buffer/single-pixel-buffer-v1.xml");
    scanner.addCustomProtocol(b.path("protocol/river-window-management-v1.xml"));
    scanner.addCustomProtocol(b.path("protocol/river-xkb-bindings-v1.xml"));
    scanner.addCustomProtocol(b.path("protocol/river-layer-shell-v1.xml"));
    scanner.addCustomProtocol(b.path("protocol/river-input-management-v1.xml"));
    scanner.addCustomProtocol(b.path("protocol/river-libinput-config-v1.xml"));
    scanner.addCustomProtocol(b.path("protocol/river-xkb-config-v1.xml"));

    scanner.generate("wl_compositor", 4);
    scanner.generate("wl_subcompositor", 1);
    scanner.generate("wl_shm", 1);
    scanner.generate("wl_seat", 7);
    scanner.generate("wl_output", 4);
    scanner.generate("wp_viewporter", 1);
    scanner.generate("wp_fractional_scale_manager_v1", 1);
    scanner.generate("wp_single_pixel_buffer_manager_v1", 1);
    scanner.generate("river_window_manager_v1", 2);
    scanner.generate("river_xkb_bindings_v1", 2);
    scanner.generate("river_layer_shell_v1", 1);
    scanner.generate("river_input_manager_v1", 1);
    scanner.generate("river_libinput_config_v1", 1);
    scanner.generate("river_xkb_config_v1", 1);

    const wayland_mod = b.createModule(.{ .root_source_file = scanner.result });
    const xkbcommon_mod = b.dependency("xkbcommon", .{}).module("xkbcommon");
    const mvzr_mod = b.dependency("mvzr", .{}).module("mvzr");

    const utils_mod = b.createModule(.{
        .root_source_file = b.path("src/utils.zig"),
        .imports = &.{
            .{ .name = "wayland", .module = wayland_mod },
        }
    });
    const rule_mod = b.createModule(.{
        .root_source_file = b.path("src/rule.zig"),
        .imports = &.{
            .{ .name = "mvzr", .module = mvzr_mod },
        },
    });
    const kwm_mod = b.createModule(.{
        .root_source_file = b.path("src/kwm.zig"),
        .imports = &.{
            .{ .name = "wayland", .module = wayland_mod },

            .{ .name = "utils", .module = utils_mod },
            .{ .name = "rule", .module = rule_mod },
        },
    });

    const config_path = b.option([]const u8, "config", "path to config file") orelse "config.zig";
    const backup_config_path = "config.def.zig";
    const config_mod = b.createModule(.{
        .root_source_file = blk: {
            fs.cwd().access(config_path, .{}) catch |err| switch (err) {
                error.FileNotFound => {
                    std.log.warn("Config file `{s}` not found, creating from `{s}`", .{ config_path, backup_config_path });

                    fs.cwd().copyFile(backup_config_path, fs.cwd(), config_path, .{}) catch |copy_err| {
                        std.log.err("Failed to copy `{s}` to `{s}`: {}", .{ backup_config_path, config_path, copy_err });
                        break :blk b.path(backup_config_path);
                    };

                    std.log.info("Config file `{s}` created successfully. Please review and customize it.", .{config_path});
                },
                else => {
                    std.log.err("access config file `{s}` failed: {}, use `{s}`", .{ config_path, err, backup_config_path });
                    break :blk b.path(backup_config_path);
                }
            };
            break :blk b.path(config_path);
        },
        .imports = &.{
            .{ .name = "wayland", .module = wayland_mod },
            .{ .name = "xkbcommon", .module = xkbcommon_mod },

            .{ .name = "utils", .module = utils_mod },
            .{ .name = "rule", .module = rule_mod },
            .{ .name = "kwm", .module = kwm_mod },
        },
    });

    rule_mod.addImport("kwm", kwm_mod);
    kwm_mod.addImport("config", config_mod);

    const bar_enabled = b.option(bool, "bar", "if enable bar") orelse true;
    if (bar_enabled) {
        const pixman_mod = b.dependency("pixman", .{}).module("pixman");
        const fcft_mod = b.dependency("fcft", .{}).module("fcft");
        kwm_mod.addImport("pixman", pixman_mod);
        kwm_mod.addImport("fcft", fcft_mod);
    }

    const options = b.addOptions();
    options.addOption(bool, "bar_enabled", bar_enabled);

    kwm_mod.addOptions("build_options", options);

    // Here we define an executable. An executable needs to have a root module
    // which needs to expose a `main` function. While we could add a main function
    // to the module defined above, it's sometimes preferable to split business
    // logic and the CLI into two separate modules.
    //
    // If your goal is to create a Zig library for others to use, consider if
    // it might benefit from also exposing a CLI tool. A parser library for a
    // data serialization format could also bundle a CLI syntax checker, for example.
    //
    // If instead your goal is to create an executable, consider if users might
    // be interested in also being able to embed the core functionality of your
    // program in their own executable in order to avoid the overhead involved in
    // subprocessing your CLI tool.
    //
    // If neither case applies to you, feel free to delete the declaration you
    // don't need and to put everything under a single module.
    const exe = b.addExecutable(.{
        .name = "kwm",
        .root_module = b.createModule(.{
            // b.createModule defines a new module just like b.addModule but,
            // unlike b.addModule, it does not expose the module to consumers of
            // this package, which is why in this case we don't have to give it a name.
            .root_source_file = b.path("src/main.zig"),
            // Target and optimization levels must be explicitly wired in when
            // defining an executable or library (in the root module), and you
            // can also hardcode a specific target for an executable or library
            // definition if desireable (e.g. firmware for embedded devices).
            .target = target,
            .optimize = optimize,
            // List of modules available for import in source files part of the
            // root module.
            .imports = &.{
                .{ .name = "wayland", .module = wayland_mod },

                .{ .name = "utils", .module = utils_mod },
                .{ .name = "kwm", .module = kwm_mod },
            },

            .link_libc = true,
        }),
    });

    exe.root_module.linkSystemLibrary("wayland-client", .{});
    exe.root_module.linkSystemLibrary("xkbcommon", .{});

    if (bar_enabled) {
        exe.root_module.linkSystemLibrary("pixman-1", .{});
        exe.root_module.linkSystemLibrary("fcft", .{});
    }

    // This declares intent for the executable to be installed into the
    // install prefix when running `zig build` (i.e. when executing the default
    // step). By default the install prefix is `zig-out/` but can be overridden
    // by passing `--prefix` or `-p`.
    b.installArtifact(exe);

    // This creates a top level step. Top level steps have a name and can be
    // invoked by name when running `zig build` (e.g. `zig build run`).
    // This will evaluate the `run` step rather than the default step.
    // For a top level step to actually do something, it must depend on other
    // steps (e.g. a Run step, as we will see in a moment).
    const run_step = b.step("run", "Run the app");

    // This creates a RunArtifact step in the build graph. A RunArtifact step
    // invokes an executable compiled by Zig. Steps will only be executed by the
    // runner if invoked directly by the user (in the case of top level steps)
    // or if another step depends on it, so it's up to you to define when and
    // how this Run step will be executed. In our case we want to run it when
    // the user runs `zig build run`, so we create a dependency link.
    const run_cmd = b.addRunArtifact(exe);
    run_step.dependOn(&run_cmd.step);

    // By making the run step depend on the default step, it will be run from the
    // installation directory rather than directly from within the cache directory.
    run_cmd.step.dependOn(b.getInstallStep());

    // This allows the user to pass arguments to the application in the build
    // command itself, like this: `zig build run -- arg1 arg2 etc`
    if (b.args) |args| {
        run_cmd.addArgs(args);
    }

    // Creates an executable that will run `test` blocks from the executable's
    // root module. Note that test executables only test one module at a time,
    // hence why we have to create two separate ones.
    const exe_tests = b.addTest(.{
        .root_module = exe.root_module,
    });

    // A run step that will run the second test executable.
    const run_exe_tests = b.addRunArtifact(exe_tests);

    // A top level step for running all tests. dependOn can be called multiple
    // times and since the two run steps do not depend on one another, this will
    // make the two of them run in parallel.
    const test_step = b.step("test", "Run tests");
    test_step.dependOn(&run_exe_tests.step);

    // Just like flags, top level steps are also listed in the `--help` menu.
    //
    // The Zig build system is entirely implemented in userland, which means
    // that it cannot hook into private compiler APIs. All compilation work
    // orchestrated by the build system will result in other Zig compiler
    // subcommands being invoked with the right flags defined. You can observe
    // these invocations when one fails (or you pass a flag to increase
    // verbosity) to validate assumptions and diagnose problems.
    //
    // Lastly, the Zig build system is relatively simple and self-contained,
    // and reading its source code will allow you to master it.
}
